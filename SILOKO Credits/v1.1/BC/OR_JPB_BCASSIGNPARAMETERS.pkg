CREATE OR REPLACE PACKAGE OR_JPB_BCAssignParameters
IS
/*
    Propiedad intelectual de Open International Systems. (c)

    Paquete       : OR_JPB_BCAssignParameters

    Descripción   : Componente de negocio relacionados a los posibles cupos asignados
                    a clientes SILOKO.

    Autor         : Jesús David Prasca Bustos
    Fecha         : 02-05-2018

    Historia de Modificaciones
    Fecha         IDEntrega


    02-05-2018    jprascaSAO414826
    Creación
*/


	-- Declaracion de Tipos de datos publicos

	-- Declaracion de variables publicas

	-- Declaracion de metodos publicos
	
 /**********************************************************************
    Propiedad intelectual de OPEN International Systems
    Nombre:         FSBVERSION
    Descripción:    Obtiene la versión del paquete
    ***********************************************************************/
    FUNCTION FSBVERSION
    RETURN  VARCHAR2;


    /**********************************************************************
    Propiedad intelectual de OPEN International Systems
    Nombre:         AssignSingleQuota
    Descripción:    Asigna un cupo manualmente a un cliente SILOKO
    ***********************************************************************/
    PROCEDURE AssignSingleQuota
    (
        inuCustomerId    IN     or_jpb_assigned_quota.customer_id%TYPE,
        inuAssigned      IN     or_jpb_assigned_quota.assigned%TYPE,
        inuSuccess       OUT    ge_message.message_id%TYPE
    );
    
    /*PROCEDURE Exist
    (
        or_jpb_parameters.country_id
        OR_jpb_parameters.city_id
        or_jpb_parameters.stratum_id
        or_jpb_parameters.active_products
        OR_jpb_parameters.inf_limit
        
        
    );*/
    
    PROCEDURE FillTbParameters
    (
        inuCityId       IN  or_jpb_parameters.city_id%type,
        otbParameterSet OUT daor_jpb_parameters.tytbOR_jpb_parameters
    );
END OR_JPB_BCAssignParameters;
/
CREATE OR REPLACE PACKAGE BODY OR_JPB_BCAssignParameters
IS

    /*
    Propiedad intelectual de Open International Systems. (c)

    Paquete       : OR_JPB_BCAssignParameters

    Descripción   : Componente de negocio relacionados a la información de los posibles
                    cupos asociados a clientes SILOKO.

    Autor         : Jesús David Prasca Bustos
    Fecha         : 02-05-2018

    Historia de Modificaciones
    Fecha         IDEntrega
    
    
    02-05-2018    jprascaSAO414826
    Creación
    */

	-- Declaracion de variables y tipos globales privados del paquete
    CSBVERSION                  CONSTANT VARCHAR2(10) := 'SAO414826';
    cnuDomainYesNot             CONSTANT ge_domain_values.domain_values_id%type := 60066;


	-- Definicion de metodos publicos y privados del paquete


/*******************************************************************************
    Propiedad intelectual de Open International Systems. (c)

    Método:       : FSBVERSION

    Descripción   : Función pública que se usa para obtener la versión del paquete

    Parámetros    :

    Retorno       : CSBVERSION - retorna la variable que contiene la versión

    Autor         : Jesús David Prasca Bustos
    Fecha         : 02-05-2018

    Historia de Modificaciones
    Fecha         IDEntrega

    02-05-2018    jprascaSAO414826
    Creación
*******************************************************************************/
    FUNCTION FSBVERSION
    RETURN VARCHAR2 IS
    BEGIN
        return CSBVERSION;
    END FSBVERSION;




/*******************************************************************************
    Propiedad intelectual de Open International Systems. (c)

    Método:       : AssignSingleQuota

    Descripción   : Método público que se usa para asignar un cupo a un cliente
                    (que no tiene cupo asigando) ignorando los criterios de
                    asignación (manualmente).

    Parámetros    : inuCustomerId   -   Identificador del cliente SILOKO
                    inuAssigned     -   Monto a asignar en el cupo

    Retorno       :
    
    Autor         : Jesús David Prasca Bustos
    Fecha         : 05-05-2018

    Historia de Modificaciones
    Fecha         IDEntrega

    05-05-2018    jprascaSAO414826
    Creación
*******************************************************************************/

    PROCEDURE AssignSingleQuota
    (
        inuCustomerId    IN     or_jpb_assigned_quota.customer_id%TYPE,
        inuAssigned      IN     or_jpb_assigned_quota.assigned%TYPE,
        inuSuccess       OUT    ge_message.message_id%TYPE
    )
    IS
        nuFindCustomer  OR_jpb_assigned_quota.customer_id%TYPE;
        rcQuota         daor_jpb_assigned_quota.styOR_jpb_assigned_quota;
    BEGIN
    
        ut_trace.trace('Inicia OR_JPB_BCAssignedQuota.AssignSingleQuota', 7);
        nuFindCustomer := 0;
        
        /* Verificando que el cliente no se encuentre entre los que ya tiene cupo asignado*/
        SELECT
            /*+ INDEX(OR_JPB_ASSIGNED_QUOTA UX_OR_JPB_ASSIGNED_QUOTA02)*/
            COUNT(1)
        INTO
            nuFindCustomer
        FROM
            OR_JPB_ASSIGNED_QUOTA
        WHERE
            OR_JPB_ASSIGNED_QUOTA.customer_id = inuCustomerId;


        /*
        Dependiendo del resultado anterior, entonces se procede a realizar la respectiva
        acción y también a modificar el mensaje que se mostrará en la aplicación
        */
        IF nuFindCustomer = 0
        THEN
            rcQuota.quota_id    :=  seq.getnext('SEQ_OR_JPB_ASSIGNED_QUO_372769');
            rcQuota.customer_id :=  inuCustomerId;
            rcQuota.assigned    :=  inuAssigned;
            rcQuota.available   :=  inuAssigned;
            rcQuota.is_enabled  :=  'Y';
            
            inuSuccess := -16000; -- se ha registrado con éxito
            daor_jpb_assigned_quota.insRecord(rcQuota);
        ELSE
            inuSuccess := -16001; -- ya tiene cupo asignado
        END IF;
        
        ut_trace.trace('Inicia OR_JPB_BCAssignedQuota.AssignSingleQuota', 7);
    EXCEPTION
        when ex.CONTROLLED_ERROR then
            ut_trace.trace('Termina ex.CONTROLLED_ERROR OR_JPB_BCAssignedQuota.AssignSingleQuota', 7);
            raise ex.CONTROLLED_ERROR;
        when others then
            ut_trace.trace('Termina others OR_JPB_BCAssignedQuota.AssignSingleQuota', 7);
            Errors.setError;
            raise ex.CONTROLLED_ERROR;
    END AssignSingleQuota;



/*******************************************************************************
    Propiedad intelectual de Open International Systems. (c)

    Método:       : FillTbParameters

    Descripción   : Método público que se usa para llenar una tabla en memoria
                    que contiene los conjuntos de parámetros de asignación asociados
                    a una ciudad.

    Parámetros    : inuCityId           - Identificador de la ciudad
                    otbParameterSet     - Tabla que se llena

    Retorno       :

    Autor         : Jesús David Prasca Bustos
    Fecha         : 05-05-2018

    Historia de Modificaciones
    Fecha         IDEntrega

    05-05-2018    jprascaSAO414826
    Creación
*******************************************************************************/
    PROCEDURE FillTbParameters
    (
        inuCityId       IN  or_jpb_parameters.city_id%type,
        otbParameterSet OUT daor_jpb_parameters.tytbOR_jpb_parameters
    )
    IS
        rfParameters    daor_jpb_parameters.tyRefCursor;
    BEGIN
        ut_trace.trace('Inicia OR_JPB_BCAssignParameters.FillTbParameters... inuCityId=>'||inuCityId, 7);
        OPEN rfParameters FOR
            SELECT
                /*+ INDEX(OR_JPB_PARAMETERS IDX_OR_JPB_PARAMETERS01) */
                or_jpb_parameters.parameter_id,
                OR_jpb_parameters.stratum_id,
                OR_jpb_parameters.city_id,
                OR_jpb_parameters.country_id,
                or_jpb_parameters.plan_type_id,
                OR_jpb_parameters.active_products,
                OR_jpb_parameters.inf_limit,
                OR_jpb_parameters.sup_limit,
                or_jpb_parameters.amount,
                OR_jpb_parameters.rowid
            FROM
                OR_jpb_parameters
            WHERE
                OR_jpb_parameters.city_id = inuCityId;

            FETCH rfParameters BULK COLLECT INTO otbParameterSet;
            ut_trace.trace('Termina OR_JPB_BCAssignParameters.FillTbParameters', 7);
        
    EXCEPTION
        when ex.CONTROLLED_ERROR then
            IF rfParameters%ISOPEN
            THEN
                CLOSE rfParameters;
            END IF;
            ut_trace.trace('Termina ex.CONTROLLED_ERROR OR_JPB_BCAssignParameters.FillTbParameters', 7);
            raise ex.CONTROLLED_ERROR;
        when others then
            IF rfParameters%ISOPEN
            THEN
                CLOSE rfParameters;
            END IF;
            ut_trace.trace('Termina others OR_JPB_BCAssignParameters.FillTbParameters', 7);
            Errors.setError;
            raise ex.CONTROLLED_ERROR;
    END FillTbParameters;
    
END OR_JPB_BCAssignParameters;
/
